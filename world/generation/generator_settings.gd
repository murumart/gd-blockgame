class_name GeneratorSettings extends Resource

## Base class for world generation settings.

const FLAG_INVALID := 0b1
const FLAG_AIR := 0b10

## A generator will go through addons before it returns the block generated by itself.
@export var addons: Array[GeneratorSettings]
## Any blocks here will be ignored during gathering of the addon block.
@export_flags("Invalid", "Air") var ignore_as_addon: int = 0b11
## Any blocks here will be ignored if it came from an addon.
@export_flags("Invalid", "Air") var ignore_from_addons: int = 0b11


func is_addon_block_ignorable(block: int) -> bool:
	return (block == BlockTypes.INVALID_BLOCK_ID and FLAG_INVALID & ignore_as_addon
			or block == BlockTypes.AIR and FLAG_AIR & ignore_as_addon)


func should_addon_block_be_ignored(block: int) -> bool:
	return (block == BlockTypes.INVALID_BLOCK_ID and FLAG_INVALID & ignore_from_addons
			or block == BlockTypes.AIR and FLAG_AIR & ignore_from_addons)


func _get_addons_block(global_position: Vector3) -> int:
	for addon in addons:
		var block := addon.get_block_at(global_position)
		if addon.is_addon_block_ignorable(block):
			continue
		return block
	return BlockTypes.INVALID_BLOCK_ID


func get_block_at(global_position: Vector3) -> int:
	var addon := _get_addons_block(global_position)
	if not should_addon_block_be_ignored(addon):
		return addon
	return BlockTypes.AIR
